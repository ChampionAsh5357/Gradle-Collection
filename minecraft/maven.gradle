/*
 This Gradle script provides the basic setup for uploading Minecraft mod artifacts to the MMD
 Maven. While this code is specific to the MMD maven, it can be modified to fit your setup.
 
 This script is set up to upload the following Jars to maven.
 - The main compiled/obfuscated jar.
 - The non obfuscated (deobf) jar.
 - The source code jar.
 - The JavaDoc jar.
 
 Requirements!
 - maven-publish Gradle plugin.
 - https://github.com/MinecraftModDevelopment/Gradle-Collection/blob/master/minecraft/artifacts.gradle
*/

publishing {

    publications {
    
        mavenJava(MavenPublication) {
        
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Adds the various artifacts 
            artifact sourcesJar {
            
                classifier 'sources'
            }
            
            artifact javadocJar {
            
                classifier 'javadoc'
            }
            
            artifact deobfJar {
            
                classifier 'deobf'
            }
        }
    }
    
    repositories {
    
        maven {
        
            credentials {
            
                username findProperty('userMaven')
                password findProperty('authMaven')
            }
            
            url findProperty('urlMaven')
        }
    }
}
